/*
 * Copyright (c) 2018 Practice Insight Pty Ltd. All Rights Reserved.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // https://github.com/GoogleContainerTools/jib/issues/1018
        classpath('org.apache.httpcomponents:httpclient:4.0.1') {
            force = true
        }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '0.10.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'maven'
    id 'net.ltgt.apt' version '0.19'
}

apply from: "$rootDir/gradle/conf/checkstyle.gradle"
apply from: "$rootDir/gradle/conf/jacoco.gradle"

group = 'io.wisetime'
archivesBaseName = 'wisetime-jira-connector'
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'io.wisetime.connector.jira.ConnectorLauncher'

jib {
    to {
        image = "wisetime/wisetime-jira-connector"
    }
}

repositories {
    mavenCentral()
}

tasks.jib.dependsOn compileJava

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}

ext.jacocoLimits = [
        'instruction': 85,
        'line'       : 85,
        'method'     : 85,
        'class'      : 85
]

dependencies {
    compile 'org.apache.commons:commons-configuration2:2.4'

    compile ('io.wisetime:wise-log-aws:3.1.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile ('com.google.inject:guice:4.2.2') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile 'com.google.guava:guava:25.1-jre'

    compile 'io.wisetime:wisetime-connector:1.1.1'

    compile 'org.codejargon:fluentjdbc:1.8.3'
    compile 'com.zaxxer:HikariCP:3.2.0'
    compile 'mysql:mysql-connector-java:5.1.44'
    compile 'org.postgresql:postgresql:42.2.2'

    compile 'ch.qos.logback:logback-core:1.2.3'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compileOnly "org.immutables:value:2.7.2:annotations"
    annotationProcessor "org.immutables:value:2.7.2"

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile 'org.mockito:mockito-core:2.22.0'
    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile 'com.github.javafaker:javafaker:0.16'
    testCompile 'org.flywaydb:flyway-core:5.1.1'
    testRuntime 'com.h2database:h2:1.4.197'
}

test {
    useJUnitPlatform()
    testLogging {
        // "passed", "skipped", "failed"
        events 'skipped', 'failed'
    }
}

clean {
    delete "${projectDir}/out"
}
