apply plugin: "jacoco"
// https://plugins.gradle.org/plugin/com.github.kt3k.coveralls
apply plugin: "com.github.kt3k.coveralls"

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
    afterEvaluate {
        // https://stackoverflow.com/questions/29887805/filter-jacoco-coverage-reports-with-gradle
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/generated/**',
                              '**/example/**',
                              '**/config/**',
                              '**/support/**',
                              '**/Main.class'
                    ])
        })
    }
    doLast {
        def xmlReport = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
        //logger.lifecycle("Checking coverage results: ${xmlReport}")

        def parser = new XmlParser()
        parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
        parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        def results = parser.parse(xmlReport)

        def percentage = {
            if (it) {
                def covered = it.'@covered' as Double
                def missed = it.'@missed' as Double
                ((covered / (covered + missed)) * 100).round(2)
            } else {
                100
            }
        }

        def counters = results.counter
        def metrics = [:]
        metrics << [
                'instruction': percentage(counters.find { it.'@type'.equals('INSTRUCTION') }),
                'line'       : percentage(counters.find { it.'@type'.equals('LINE') }),
                'method'     : percentage(counters.find { it.'@type'.equals('METHOD') }),
                'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })

                // branch and complexity measures of current version of jacoco tool perform poorly
                // when using functional style of programming under Java 8, and are no longer mandatory targets
                // 'branch'     : percentage(counters.find { it.'@type'.equals('BRANCH') }),
                // 'complexity' : percentage(counters.find { it.'@type'.equals('COMPLEXITY') }),
        ]

        def failures = []
        def coverageRates = []
        metrics.each {
            def limit = jacocoLimits[it.key]
            if (it.value < limit) {
                failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
            }
            coverageRates.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
        }

        def htmlReport = file("${jacoco.reportsDir}/test/html/index.html")
        if (failures) {
            logger.quiet("------------------ Code Coverage Failed -----------------------")
            failures.each {
                logger.quiet(it)
            }
            logger.quiet("------------------ All Code Coverage Rates --------------------")
            coverageRates.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
            logger.quiet("For updating your Jacoco limits, please copy the following:")
            logger.quiet("\next.jacocoLimits = [")
            metrics.eachWithIndex { item, index ->
                if (index < metrics.size() - 1) {
                    logger.quiet("        '${item.key}': ${item.value},")
                } else {
                    logger.quiet("        '${item.key}': ${item.value}")
                }
            }
            logger.quiet("]\n")
            logger.quiet("---------------------------------------------------------------")
            logger.lifecycle("Please check coverage report: ${htmlReport}")
            throw new GradleException("Code coverage failed")
        } else {
            logger.quiet("------------------ Code Coverage Passed -----------------------")
            coverageRates.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
            logger.quiet("Passed Code Coverage Checks: ${htmlReport}")
        }
    }
}
check.dependsOn jacocoTestReport

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    finalizedBy jacocoTestReport
}
